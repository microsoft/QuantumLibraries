// Copyright (c) Microsoft Corporation. All rights reserved.
// Licensed under the MIT License.

namespace Microsoft.Quantum.Chemistry.Tests {
    
    open Microsoft.Quantum.Primitive;
    open Microsoft.Quantum.Extensions.Testing;
    open Microsoft.Quantum.Extensions.Math;
    open Microsoft.Quantum.Extensions.Convert;
    open Microsoft.Quantum.Chemistry.JordanWigner; 
    open Microsoft.Quantum.Diagnostics;
    open Microsoft.Quantum.Canon;
    open Microsoft.Quantum.Preparation;
    open Microsoft.Quantum.Arithmetic;
    open Microsoft.Quantum.Math;
    open Microsoft.Quantum.Arrays;
    
    // Check that correct Pauli Z string is computed
    function _ComputeJordanWignerBitString_0Test() : Unit {
        let nFermions = 5;
        let fermionIndices = [0, 3];
        let expectedBitString = [false, true, true, false,false];
        let bitString = _ComputeJordanWignerBitString(nFermions, fermionIndices);
        AllEqualityFactB (bitString, expectedBitString, "Bit strings not equal");
    }

    function _ComputeJordanWignerBitString_1Test() : Unit {
        let nFermions = 7;
        let fermionIndices = [0, 4, 2, 6];
        let expectedBitString = [false, true, false, false, false, true, false];
        let bitString = _ComputeJordanWignerBitString(nFermions, fermionIndices);
        AllEqualityFactB (bitString, expectedBitString, "Bit strings not equal");
    }

    function _ComputeJordanWignerPauliZString_0Test() : Unit {
        let nFermions = 7;
        let fermionIndices = [0, 4, 2, 6];
        let expectedBitString = [PauliI, PauliZ, PauliI, PauliI, PauliI, PauliZ, PauliI];
        let bitString = _ComputeJordanWignerPauliZString(nFermions, fermionIndices);
        
        mutable product = new Bool[nFermions];
        mutable expected = new Bool[nFermions];
        for(idx in 0..nFermions - 1){
            set product w/= idx <- expectedBitString[idx] == bitString[idx] ? true | false;
            set expected w/= idx <- true;
        }
        
        AllEqualityFactB (product, expected, "Bit strings not equal");
    }

    function _JordanWignerClusterOperatorPQRSTermSignsTestHelper(idx : Int) : (Int[], Double[], Double){
        let cases = [
        ([1,2,3,4],[1.0,-1.0,-1.0,-1.0,1.0,1.0,1.0,-1.0],1.0),
        ([2,1,4,3],[1.0,-1.0,-1.0,-1.0,1.0,1.0,1.0,-1.0],1.0),
        ([3,4,1,2],[1.0,-1.0,-1.0,-1.0,1.0,1.0,1.0,-1.0],-1.0),
        ([2,1,3,4],[1.0,-1.0,-1.0,-1.0,1.0,1.0,1.0,-1.0],-1.0),
        ([1,3,2,4],[-1.0,-1.0,-1.0,1.0,-1.0,1.0,1.0,1.0],1.0),
        ([4,2,3,1],[-1.0,-1.0,-1.0,1.0,-1.0,1.0,1.0,1.0],-1.0),
        ([1,4,2,3],[1.0,1.0,-1.0,1.0,-1.0,1.0,-1.0,-1.0],1.0),
        ([2,3,4,1],[1.0,1.0,-1.0,1.0,-1.0,1.0,-1.0,-1.0],1.0)
        ];
        return cases[idx];
    }

    function _JordanWignerClusterOperatorPQRSTermSignsTest() : Unit{
        for (idx in 0..7) {
            let (testCase, expectedSigns, expectedGlobalSign) = _JordanWignerClusterOperatorPQRSTermSignsTestHelper(idx);
            let (sortedIndices, signs, globalSign) = _JordanWignerClusterOperatorPQRSTermSigns(testCase);

            let p = sortedIndices[0];
            let q = sortedIndices[1];
            let r = sortedIndices[2];
            let s = sortedIndices[3];

            Fact(p<q and q<r and r<s, "Expected p<q<r<s");
            NearEqualityFact(globalSign, expectedGlobalSign);
            for (signIdx in 0..Length(signs)-1) {
                NearEqualityFact(signs[signIdx], expectedSigns[signIdx]);
            }
        }
    }

    function _DoublesToComplexPolar(input: Double[]) : ComplexPolar[]{
        mutable arr = new ComplexPolar[Length(input)];
        for(idx in 0..Length(input)-1){
            set arr w/= idx <- ComplexToComplexPolar(Complex((input[idx],0.)));
        }
        return arr;
    }

    operation _JordanWignerUCCTermTestHelper(nQubits: Int, excitations: Int[], term: JordanWignerInputState[], result: Double[]) : Unit{
        using(qubits = Qubit[nQubits]){
            for(idx in excitations){
                X(qubits[idx]);
            }
            PrepareUnitaryCoupledClusterState (NoOp<Qubit[]>, term, 1.0, qubits);
            DumpRegister ((), qubits);
            (Adjoint PrepareArbitraryState)(_DoublesToComplexPolar(result), LittleEndian(qubits));
            AssertAllZeroWithinTolerance (qubits, 1e-5);
            ResetAll(qubits);
        }
    }

    operation JordanWignerUCCSTermTest() : Unit{
        // test using Exp(2.0 (a^\dag_1 a_3 - h.c.)) 
        let term0 = [JordanWignerInputState((2.0,0.0), [1,3])];
        let state0 = [0.,0.,-0.416147,0.,0.,0.,0.,0.,-0.909297,0.,0.,0.,0.,0.,0.,0.];
        _JordanWignerUCCTermTestHelper(4, [1], term0, state0);

        // test using Exp(2.0 (a^\dag_3 a_1 - h.c.)) 
        let term1 = [JordanWignerInputState((2.0,0.0), [3,1])];
        let state1 = [0.,0.,-0.416147,0.,0.,0.,0.,0.,0.909297,0.,0.,0.,0.,0.,0.,0.];
        _JordanWignerUCCTermTestHelper(4, [1], term1, state1);
    }

    operation JordanWignerUCCDTermPQRSTest() : Unit{
        // test using Exp(2.0 (a^\dag_0 a^\dag_1 a_3 a_4 - h.c.)) 
        let term0 = [JordanWignerInputState((2.0,0.0), [0,1,2,4])];
        let state0 = [0.,0.,0.,-0.416147,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.909297,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.];
        _JordanWignerUCCTermTestHelper(5, [0,1], term0, state0);

        // test using Exp(2.0 (a^\dag_0 a^\dag_1 a_3 a_4 - h.c.)) 
        let term1 = [JordanWignerInputState((2.0,0.0), [0,1,2,4])];
        let state1 = [0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,-0.416147,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,-0.909297,0.,0.,0.];
        _JordanWignerUCCTermTestHelper(5, [0,1,3], term1, state1);

        // test using Exp(2.0 (a^\dag_1 a^\dag_0 a_2 a_4 - h.c.)) 
        let term2 = [JordanWignerInputState((2.0,0.0), [1,0,2,4])];
        let state2 = [0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,-0.416147,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.909297,0.,0.,0.];
        _JordanWignerUCCTermTestHelper(5, [0,1,3], term2, state2);

        // test using Exp(2.0 (a^\dag_1 a^\dag_0 a_2 a_4 - h.c.)) 
        let term3 = [JordanWignerInputState((-2.0,0.0), [4,2,1,0])];
        let state3 = [0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,-0.416147,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.909297,0.,0.,0.];
        _JordanWignerUCCTermTestHelper(5, [0,1,3], term2, state2);
    }

    operation JordanWignerUCCDTermPRSQTest() : Unit {
        let term0 = [JordanWignerInputState((2.0,0.0), [0,3,1,2])];
        let state0 = [0.,0.,0.,0.,0.,0.,0.909297,0.,0.,-0.416147,0.,0.,0.,0.,0.,0.];
        //_JordanWignerUCCTermTestHelper(5, [0,3], term0, state0);
    }

    operation JordanWignerUCCDTermPRQSTest() : Unit {
        // test using Exp(2.0 (a^\dag_2 a^\dag_0 a_4 a_1 - h.c.)) 
        let term3 = [JordanWignerInputState((2.0,0.0), [2,0,3,1])];
        let state3 = [0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,-0.416147,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,0.,-0.909297,0.,0.,0.,0.,0.];
        //_JordanWignerUCCTermTestHelper(4, [0,1], term3, state3);
    }

}



