namespace Microsoft.Quantum.Canon {
    open Microsoft.Quantum.Extensions.Math;
    
    /// A simulation technique converts an EvolutionGenerator to time evolution by some time step
    /// Here is an example of a simulation technique. 
    operation TrotterStepImpl(evolutionGenerator: EvolutionGenerator, idx : Int, stepsize: Double, qubits: Qubit[]) : () {
        body {
            let (evolutionSet, generatorSystem) = evolutionGenerator;
            let (nTerms, generatorSystemFunction) = generatorSystem;
            let generatorIndex = generatorSystemFunction(idx);
            (evolutionSet(generatorIndex))(stepsize, qubits);
        }
        adjoint auto
        controlled auto
        controlled adjoint auto
    }
    function TrotterStep(evolutionGenerator: EvolutionGenerator, trotterOrder: Int, trotterStepSize: Double) : (Qubit[] => () :  Adjoint, Controlled)
    {
        let (evolutionSet, generatorSystem) = evolutionGenerator;
        let (nTerms, generatorSystemFunction) = generatorSystem;
        // The input to DecomposeIntoTimeStepsCA has signature
        // (Int, ((Int, Double, Qubit[]) => () : Adjoint, Controlled))
        let trotterForm = (nTerms, TrotterStepImpl(evolutionGenerator, _, _, _));
        return (DecomposeIntoTimeStepsCA(trotterForm,trotterOrder))(trotterStepSize, _);
    }

    /// This simulation algorithm takes (timeMax, EvolutionGenerator, 
    /// register) and other algorithm-specific parameters (trotterStepSize, 
    /// trotterOrder), and performs evolution under the EvolutionGenerator 
    /// for time = timeMax.
    operation TrotterSimulationAlgorithmImpl(trotterStepSize: Double, 
                                                trotterOrder: Int, 
                                                maxTime: Double, 
                                                evolutionGenerator: EvolutionGenerator, 
                                                qubits:Qubit[]) : () {
        body{
            let nTimeSlices = Ceiling(maxTime / trotterStepSize);
            let resizedTrotterStepSize = maxTime / Float(nTimeSlices);
            for (idxTimeSlice in 0..nTimeSlices-1) {
                (TrotterStep(evolutionGenerator, trotterOrder, resizedTrotterStepSize))(qubits);
            }
        }

        adjoint auto
        controlled auto
        controlled adjoint auto
    }
    function TrotterSimulationAlgorithm(trotterStepSize: Double,
                                        trotterOrder: Int) : SimulationAlgorithm
    {
        return SimulationAlgorithm(TrotterSimulationAlgorithmImpl(trotterStepSize,trotterOrder, _, _, _));
    }
    
    /// This simple time-depedendent simulation algorithm implements a
    /// sequence of uniformly-sized trotter steps
    operation TimeDependentTrotterSimulationAlgorithmImpl(  trotterStepSize: Double, 
                                                            trotterOrder: Int, 
                                                            timeMax: Double, 
                                                            evolutionSchedule: EvolutionSchedule,
                                                            qubits:Qubit[]) : () {
        body {
            let nTimeSlices = Ceiling(timeMax / trotterStepSize);
            let resizedTrotterStepSize = timeMax / Float(nTimeSlices);
            for(idxTimeSlice in 0..nTimeSlices-1){
                let schedule = Float(idxTimeSlice) / Float(nTimeSlices);
                let (evolutionSet, generatorSystemTimeDependent) = evolutionSchedule;
                let generatorSystem = generatorSystemTimeDependent(schedule);
                let evolutionGenerator = EvolutionGenerator(evolutionSet, generatorSystem);
                (TrotterSimulationAlgorithm(resizedTrotterStepSize, trotterOrder))(resizedTrotterStepSize, evolutionGenerator, qubits);
            }
        }

        adjoint auto
        controlled auto
        controlled adjoint auto
    }

    function TimeDependentTrotterSimulationAlgorithm(   trotterStepSize: Double, 
                                                        trotterOrder: Int) : SimulationAlgorithmTimeDependent {
        return SimulationAlgorithmTimeDependent(TimeDependentTrotterSimulationAlgorithmImpl(trotterStepSize,trotterOrder, _, _, _));
    }    

    //For LCU
}

